# Maintainer: Bart De Vries <bart at mogwai dot be>
# Contributor: Dan Johansen <strit@manjaro.org>

pkgname=box64-armv9
pkgver=0.4.0
pkgrel=1
pkgdesc='Linux Userspace x86_64 Emulator with a twist'
arch=('aarch64')
url='https://github.com/ptitSeb/box64'
license=('MIT')
install="box64.install"
depends=('gcc-libs')
makedepends=('git' 'cmake' 'python')
options=('!strip')
provides=('box64')
confilcts=('box64')
source=("box64-${pkgver}.tar.gz::https://github.com/ptitSeb/box64/archive/v${pkgver}.tar.gz"
        "box64.install")
sha256sums=('4504b0df2d9ccfcf15b4d0c80664e216dc1170a4f8caeca46e79b93ff47e5e87'
            '7e94518dbd11121f150a51b64f4c0ec11f844a83f7b15205d28c1de63de699f2')

build() {
    cd box64-${pkgver}
    if [[ $CARCH == "aarch64" ]]; then
        name="$(LC_ALL=C lscpu | grep Model)"
        exargs=""
        if [ -n "$(echo $name | grep RK3588)" ]; then
              exargs="-DRK3588=1"
        elif [ -n "$(echo $name | grep RK3399)" ]; then
              exargs="-DRK3399=1"
        elif [ -n "$(echo $name | grep 'Cortex-A53')" ]; then
              exargs="-DRPI3ARM64=1"
        elif [ -n "$(echo $name | grep 'Cortex-A72')" ]; then
              exargs="-DRPI4ARM64=1"
        elif [ -n "$(echo $name | grep 'Cortex-A76')" ]; then
              exargs="-DRPI5ARM64=1"
        fi
        cmake -B build -S . \
              $exargs \
              -DBOX32=OFF \
              -DBOX32_BINFMT=OFF \
              -DARM_DYNAREC=ON \
              -DCMAKE_BUILD_TYPE=RelWithDebInfo \
              -DCMAKE_INSTALL_PREFIX=/usr
    fi
    make -C build
}

package() {
    cd box64-${pkgver}/build
    if [[ $CARCH == "aarch64" ]]; then
      make DESTDIR=${pkgdir} install
    elif [[ $CARCH == "x86_64" ]]; then
      install -Dm755 box64 -t "${pkgdir}/usr/bin/"
    elif [[ $CARCH == "powerpc64le" ]]; then
      make DESTDIR=${pkgdir} install
    elif [[ $CARCH == "riscv64" ]]; then
      make DESTDIR=${pkgdir} install
    fi

    install -Dm644 ../LICENSE -t "${pkgdir}/usr/share/licenses/box64/"

    # Install documentation
    install -d "${pkgdir}/usr/share/doc/box64/"
    cp -R ../docs/* "${pkgdir}/usr/share/doc/box64/"
}
