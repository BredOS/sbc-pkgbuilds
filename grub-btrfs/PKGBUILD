# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: gato_lento <vukk.euob at gmail dot com>
# Contributor: James W. Barnett <james at wbarnett dot us>

pkgname=grub-btrfs
pkgver=4.13
pkgrel=3
pkgdesc='Include btrfs snapshots in GRUB boot options'
arch=('any')
url="https://github.com/Antynea/${pkgname}"
license=('GPL3')
depends=('btrfs-progs' 'grub>=2.12.bredos')
optdepends=(
    'snapper: For snapper support'
    'inotify-tools: For grub-btrfsd daemon'
)
backup=('etc/default/grub-btrfs/config')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "41_snapshots-btrfs")
b2sums=('e2f11a0a8282e3ec8ff8223e3bad70b5d5c5e81b4d740a1bf3554db412dbe48a8a0f216f4e6c65ae1d11c01a27b8b92bae9b470c60d2389505ce089511536e1a'
        '692c360095297ef46ca7474e10030d332b756292f01e65cab4aa9bb252bcc5f24c81da57792c3c8cbfd89404689d59a49de488ec5da40c03326b3448861a4a3b')

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" INITCPIO=true install
    install -Dm777 "${srcdir}/41_snapshots-btrfs" "${pkgdir}/etc/grub.d/41_snapshots-btrfs"
}

# vim:set ts=4 sw=4 et:
