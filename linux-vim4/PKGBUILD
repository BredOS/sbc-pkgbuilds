# Maintainer: bill88t <bill88t@feline.gr>

_target=vim4
_pkgbase="linux-$_target"
pkgbase="$_pkgbase"
pkgname=("$pkgbase" "$pkgbase-headers")
pkgver=5.4.180
_khadasver=1.5
_kernver="$pkgver"
pkgrel=3
arch=('aarch64')
_desc="AArch64 multi-platform $_target"
url="https://bredos.org/"
license=('GPL2')
options=('!strip')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
source=(
    "linux.preset"
    "khadas-vim4-linux-5.4-dt-overlays_${_khadasver}_arm64.deb"
    "linux-dtb-amlogic-5.4_${_khadasver}_arm64.deb"
    "linux-headers-amlogic-5.4_${_khadasver}_arm64.deb"
    "linux-image-amlogic-5.4_${_khadasver}_arm64.deb"
    )
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

noextract=("${source[@]##*/}")

prepare() {
  cd "$srcdir"

  rm -rf $(find -mindepth 1 -maxdepth 1 -type d)
}

_package() {
  pkgdesc="The Linux Kernel and modules - $_desc"
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7')
  optdepends=('crda: to set the correct wireless channels of your country')
  backup=("etc/mkinitcpio.d/$_pkgbase.preset")
  provides=("linux=${pkgver}" 'WIREGUARD-MODULE')
  replaces=('linux-armv8')
  conflicts=('linux')

  cd "$srcdir"
  

  ar x "khadas-vim4-linux-${_kernver%.*}-dt-overlays_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz
  ar x "linux-dtb-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz
  ar x "linux-image-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz


  install -dm755 "$pkgdir/boot/dtb/amlogic/overlays"
  cp -r "boot/dtb-$_kernver/"* "$pkgdir/boot/dtb/"
  cp -r "boot/overlays/kvim4.dtb.overlays/"* "$pkgdir/boot/dtb/amlogic/overlays"
  # prefix every file in $pkgdir/boot/dtb/amlogic/overlays/ with 'kvim4-'
  for _file in "$pkgdir/boot/dtb/amlogic/overlays/"*; do
    mv "$_file" "$pkgdir/boot/dtb/amlogic/overlays/kvim4-$(basename "$_file")"
  done
  install -dm755 "$pkgdir/usr"
  cp -r lib "$pkgdir/usr/lib"

  # sed expression for following substitutions
  local _subst="
    s|%PKGBASE%|$_pkgbase|g
    s|%KERNVER%|$_kernver|g
  "

  # install mkinitcpio preset file
  sed "$_subst" linux.preset |
    install -Dm644 /dev/stdin "$pkgdir/etc/mkinitcpio.d/$_pkgbase.preset"

  # install boot image
  install -Dm644 "boot/vmlinuz-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/vmlinuz"
  install -Dm644 "boot/vmlinuz-$_kernver" "$pkgdir/boot/vmlinuz-linux-vim4"
  install -Dm644 "boot/config-$_kernver" "$pkgdir/boot/config-$_kernver"
  install -Dm644 "boot/System.map-$_kernver" "$pkgdir/boot/System.map-$_kernver"

  # used by mkinitcpio to name the kernel
  echo "$_pkgbase" | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/pkgbase"
}

_package-headers() {
  pkgdesc="Header files and scripts for building modules for linux kernel - $_desc"
  provides=("linux-headers=${pkgver}")
  conflicts=('linux-headers')

  cd "$srcdir"
  
  ar x "linux-image-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz
  ar x "linux-headers-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz

  install -dm755 "$pkgdir/usr/lib/modules/$_kernver"
  cp -r "usr/src/linux-headers-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build"

  install -Dm644 "boot/config-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build/.config"
  install -Dm644 "boot/System.map-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build/System.map"

  # add real version for building modules and running depmod from hook
  echo "$_kernver" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/build/version"
}

for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done
