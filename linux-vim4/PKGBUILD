# Maintainer: Panda <panda@bredos.org>

_target=vim
_pkgbase="linux-$_target"
pkgbase="$_pkgbase"
pkgname=("$pkgbase" "$pkgbase-headers")
pkgver=5.15.137
_khadasver=1.7.4
_kernver="$pkgver"
pkgrel=1
arch=('aarch64')
_desc="AArch64 multi-platform $_target"
url="https://bredos.org/"
license=('GPL2')
depends=('vim4-post-install>=20250628-1')
makedepends=('dtc')
options=('!strip')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
source=(
    "linux.preset"
    "linux-dtb-amlogic-5.15_${_khadasver}_arm64.deb"
    "linux-headers-amlogic-5.15_${_khadasver}_arm64.deb"
    "linux-image-amlogic-5.15_${_khadasver}_arm64.deb"
    "panfrost.dtbo"
    )
md5sums=('1c8a066b30e962639bf055eb1b0e94c3'
         'a35594c8093fed0558d1abaecbb5ac93'
         '352d512b703832b8721cf94698e6d539'
         'd196d1ea77c524e559b2fc03ea739159'
         '49defaf88e444ed82f4cb75a1f87e082')
noextract=("${source[@]##*/}")

prepare() {
  cd "$srcdir"

  rm -rf $(find -mindepth 1 -maxdepth 1 -type d)
}

_package() {
  pkgdesc="The Linux Kernel and modules - $_desc"
  depends=('coreutils' 'linux-firmware' 'kmod' 'mkinitcpio>=0.7')
  optdepends=('crda: to set the correct wireless channels of your country')
  backup=("etc/mkinitcpio.d/$_pkgbase.preset")
  provides=("linux=${pkgver}" 'WIREGUARD-MODULE')
  replaces=('linux-armv8')
  conflicts=('linux')

  cd "$srcdir"
  

  # ar x "khadas-vim4-linux-${_kernver%.*}-dt-overlays_${_khadasver}_arm64.deb"
  # tar -xf data.tar.xz
  ar x "linux-dtb-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz
  ar x "linux-image-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz


  # install -dm755 "$pkgdir/boot/dtbs/amlogic/overlays"
  install -dm755 "$pkgdir/boot/dtbs/amlogic"
  cp -r "boot/dtb-$_kernver/"* "$pkgdir/boot/dtbs/"
  # cp -r "boot/overlays/kvim4.dtb.overlays/"* "$pkgdir/boot/dtbs/amlogic/overlays"
  # prefix every file in $pkgdir/boot/dtbs/amlogic/overlays/ with 'kvim4-'
  # for _file in "$pkgdir/boot/dtbs/amlogic/overlays/"*; do
  #   mv "$_file" "$pkgdir/boot/dtbs/amlogic/overlays/kvim4-$(basename "$_file")"
  # done

  for dtb in "$pkgdir/boot/dtbs/amlogic/"kvim4*.dtb; do
    fdtoverlay -i "$dtb" -o "$dtb" -v ${srcdir}/panfrost.dtbo 
  done


  install -dm755 "$pkgdir/usr"
  cp -r lib "$pkgdir/usr/lib"

  # sed expression for following substitutions
  local _subst="
    s|%PKGBASE%|$_pkgbase|g
    s|%KERNVER%|$_kernver|g
  "

  # install mkinitcpio preset file
  sed "$_subst" linux.preset |
    install -Dm644 /dev/stdin "$pkgdir/etc/mkinitcpio.d/$_pkgbase.preset"

  # install boot image
  install -Dm644 "boot/vmlinuz-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/vmlinuz"
  install -Dm644 "boot/vmlinuz-$_kernver" "$pkgdir/boot/vmlinuz-linux-vim4"

  # used by mkinitcpio to name the kernel
  echo "$_pkgbase" | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/pkgbase"
}

_package-headers() {
  pkgdesc="Header files and scripts for building modules for linux kernel - $_desc"
  provides=("linux-headers=${pkgver}")
  conflicts=('linux-headers')

  cd "$srcdir"
  
  ar x "linux-image-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz
  ar x "linux-headers-amlogic-${_kernver%.*}_${_khadasver}_arm64.deb"
  tar -xf data.tar.xz

  install -dm755 "$pkgdir/usr/lib/modules/$_kernver"
  cp -r "usr/src/linux-headers-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build"

  install -Dm644 "boot/config-$_kernver" "$pkgdir/usr/lib/modules/$_kernver/build/.config"

  # add real version for building modules and running depmod from hook
  echo "$_kernver" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules/$_kernver/build/version"
}

for _p in "${pkgname[@]}"; do
  eval "package_$_p() {
    $(declare -f "_package${_p#$pkgbase}")
    _package${_p#$pkgbase}
  }"
done
