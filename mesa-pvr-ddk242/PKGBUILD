# Maintainer: Chaiwat Suttipongsakul <cwt@bashell.com>

pkgname=mesa-pvr-ddk242
pkgdesc="Mesa wrapper for PVR DDK 24.2 blobs"
pkgver=24.0.9
pkgrel=1
arch=('riscv64')
makedepends=('git' 'python-mako' 'xorgproto'
              'libxml2' 'libx11'  'libvdpau' 'libva' 'elfutils' 'libxrandr'
              'wayland-protocols' 'meson' 'ninja' 'glslang' 'pandoc-cli')
depends=('mesa' 'k1-img-gpu')
url="https://www.mesa3d.org"
license=('MIT AND Khronos AND SGI-Free-Software-License-B AND Boost-permissive')
_srcname=mesa-${pkgver}
source=("git+https://gitlab.freedesktop.org/StaticRocket/mesa.git#commit=82e6a9293c476267417c5b6b906b01fb73a34e38"
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0001-redirect-glapi.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0002-change-LIBGL_ALWAYS_SOFTWARE-to-PVRGL_ALWAYS_SOFTWAR.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0003-Revert-gbm-add-gbm_bo_blit.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0004-gbm-backend-ize.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0005-add-dri-aliases.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/0006-hardcode-supported-display-drivers.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1001-glx-dri3-handle-zink-fallback-if-loader-picks-it.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1002-zink-do-not-expose-quad-support-when-no-geometry-sha.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1003-zink-emit-consts-as-uint-only-on-IMG-proprietary-dri.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1004-zink-use-lazy-descriptors-for-IMG-proprietary-driver.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1005-zink-Do-not-use-demote-on-IMG-blobs.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1006-zink-don-t-assert-geometryShader-for-IMG-proprietary.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1007-zink-skip-empty-VkSubmitInfo-when-submitting.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/1008-zink-try-to-merge-two-VkSubmitInfo-s-for-Imagination.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/2001-update-to-mesa-24.0.9.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/3001-Revert-vulkan-wsi-use-can_present_on_device-for-Wayl.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/3002-Revert-vulkan-wsi-make-the-display-FD-available.patch'
        'https://github.com/Icenowy/aosc-os-pvr/raw/refs/heads/master/ddk242/mesa-pvr-ddk242/autobuild/patches/9999-HACK-glxext-default-to-Zink.patch'
)
sha256sums=('4024e6ba91f1610b8ef72fb2bfc12cfed4f50a03fc544085d80d45ab8b31d58a'
            'd73c4f6a846d43dc9ce912aa85dd30e125cd084a979d53022852f69b570c2201'
            'aa4c8722a725526cd334d4684e3b2955c27f121874494b7d77c8e070962259df'
            '39820b48348957af2bfe7c4274527eb51efd1bba91a5ccb019f60da74a5b8279'
            '6486d7257ba3d8935a587d0d8233825706019e15348b86d4619ec92911a22ac9'
            'ba722084238a69ef123579eb02c49313a72a594d21a77b9029ac8e015eca86dd'
            '7cd0043e1500a10be575c26a9b707920e995e1ddd4e35ba15fbfa8676d0f60bd'
            'b84dd6f1c51581983059b260a91751d6e28c91d416165082cfe24e70bc327902'
            '1fde6b98ea3dab4d3667f90ea922c443b10868970ef2fec93bede99bc8a1e479'
            'b0e9756436637872ed92c154102fe9a23468555d4e4b185fb5917c81dba8132f'
            'a95a92b3e812a7fb67dbdc4ce3d4e059f9ccc1a22f94bf412c6ee0a66fa0bcac'
            'aea1cb205f205bfca129700e57662e391ad29bafa5f5bc4febd73cd79a5b139f'
            '110cade15853797d4592e4ab793dbd12c5e6b3bafebc4f346403e51bf38f5f8b'
            '964f9fb3f878ae74d3fe68656546cc6a3528df851a97b96244f346e5cc445921'
            'ff8c53dbedd14064a704e1ae590b494f3bcaf149aa31ef84a7ae431aa7901151'
            '6f44b13eb1f913cfe522f69a706b683ad0f69dc51563e924456289d6d7d81bfc'
            '61150cec8be968dbeb94a0bb17402d8f9150471a95dc3afcfc776164778e81fe'
            'ca371f9ebcc304966d0fab48f2fad56ef09056ae62a6c01bb7fa75531e88de5a'
            '9df6631177cdfd6750822a29447d3a9ad8f6b3e89ce40ba78b18705df01b3cdc')

prepare() {
    # although removing _build folder in build() function feels more natural,
    # that interferes with the spirit of makepkg --noextract
    if [  -d _build ]; then
        rm -rf _build
    fi

    local _patchfile
    for _patchfile in "${source[@]}"; do
        _patchfile="${_patchfile%%::*}"
        _patchfile="${_patchfile##*/}"
        [[ $_patchfile = *.patch ]] || continue
        echo "Applying patch $_patchfile..."
        patch --directory="${_srcname}" --forward --strip=1 --input="${srcdir}/${_patchfile}"
    done
}

build () {
    cd ${srcdir}
    mkdir _build
    cd _build
    meson setup ${srcdir}/${_srcname} --prefix=/usr -Ddri-drivers-path=/usr/lib/dri \
        -Dglvnd=true -Dglvnd-vendor-name=pvr \
        -Dgallium-drivers=pvr -Dvulkan-drivers=pvr \
        -Dglx=disabled -Dllvm=disabled -Dgbm=enabled
    ninja $NINJAFLAGS -C .
}

package() {
    mkdir -p ${pkgdir}/usr/lib ${pkgdir}/usr/share/glvnd/egl_vendor.d
    DESTDIR=${srcdir}/tmp ninja $NINJAFLAGS -C ${srcdir}/_build install
    cp -a ${srcdir}/tmp/usr/lib/*pvr* ${pkgdir}/usr/lib/
    cp -a ${srcdir}/tmp/usr/lib/dri ${pkgdir}/usr/lib/
    cp -r ${srcdir}/tmp/usr/lib/gbm ${pkgdir}/usr/lib/
    ln -sf pvr_gbm.so ${pkgdir}/usr/lib/gbm/starfive_gbm.so
    cp ${srcdir}/tmp/usr/share/glvnd/egl_vendor.d/50_pvr.json ${pkgdir}/usr/share/glvnd/egl_vendor.d/40_pvr.json
    mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
    pandoc -f rst -t plain ${srcdir}/${_srcname}/docs/license.rst -o ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
