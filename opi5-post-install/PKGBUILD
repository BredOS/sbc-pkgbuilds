# Maintainer: Panda <panda@bredos.org>

pkgbase=opi5-post-install
pkgname=('opi5-post-install' 'opi5plus-post-install' 'opi5max-post-install' 'opi5ultra-post-install' 'opi5pro-post-install' 'opi-base-cm5-post-install')
pkgver=20$(date +%y%m%d)
pkgrel=1
pkgdesc="Fixes for the Orange Pi 5"
arch=('any')
url="https://www.bredos.org"
license=('GPL')
source=('aliases.conf'
        'HDMI-OUT.conf'
        'enable-usb.service'
        'Hdmi.conf'
        'rockchip-hdmi1.conf'
        '50-suspend-state.conf'
        'setupsuspend.service')
md5sums=('56d4c13bf781296f0e48e5ddfed7aa02'
         '5e067044768315cefe60d1a26cad9a8e'
         'fddfe71c8b20c21c06b23ab6dbd3b4d0'
         'b14a8bea2713a5879bf3c5f94a5e2f75'
         'bdfa01c9a11fb5b6d30f1a8b8fd47dee'
         'b148e9c47913db49527234c834fb9338'
         '6630384b7d48987297f8d7d0496aaaad')
install=${pkgname}.install

pkgforall() {
    install -Dm644 "${srcdir}/50-suspend-state.conf" "${pkgdir}/usr/lib/systemd/system/sleep.conf.d"
    install -Dm644 "${srcdir}/setupsuspend.service" "${pkgdir}/usr/lib/systemd/system/setupsuspend.service"
}

package_opi5-post-install() {
    pkgdesc="Fixes for the Orange Pi 5"
    depends=('ap6275p-firmware')
    pkgforall
    install -D -m 0644 "${srcdir}/aliases.conf" "${pkgdir}/usr/share/alsa/cards/aliases.conf.new"
    install -D -m 0644 "${srcdir}/HDMI-OUT.conf" "${pkgdir}/usr/share/alsa/cards/HDMI-OUT.conf"
    install -D -m 0644 "${srcdir}/enable-usb.service" "${pkgdir}/usr/lib/systemd/system/enable-usb.service"
}

package_opi5plus-post-install() {
    pkgdesc="Fixes for the Orange Pi 5 Plus"
    pkgforall
    install -Dm644 ${srcdir}/Hdmi.conf ${pkgdir}/usr/share/alsa/ucm2/rockchip-hdmi1/Hdmi.conf
    install -Dm644 ${srcdir}/rockchip-hdmi1.conf ${pkgdir}/usr/share/alsa/ucm2/rockchip-hdmi1/rockchip-hdmi1.conf
}

package_opi5max-post-install() {
    pkgdesc="Fixes for the Orange Pi 5 Max"
    depends=('ap6611s-firmware')
    pkgforall
}

package_opi5ultra-post-install() {
    pkgdesc="Fixes for the Orange Pi 5 Ultra"
    depends=('ap6611s-firmware')
    pkgforall
}

package_opi5pro-post-install() {
    pkgdesc="Fixes for the Orange Pi 5 Pro"
    depends=('ap6256-firmware')
    pkgforall
}

package_opi-base-cm5-post-install() {
    pkgdesc="Fixes for the Orange Pi Base Board CM5"
    install -D -m 0644 "${srcdir}/enable-usb.service" "${pkgdir}/usr/lib/systemd/system/enable-usb.service"
    pkgforall
}

